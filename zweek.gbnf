root        ::= (definition ws)+
ws          ::= [ \t\n]*

definition  ::= "(" ws "def" ws symbol ws "(" ws args ws ")" ws type ws body ws ")"

args        ::= (arg ws)*
arg         ::= "(" ws symbol ws type ws ")"

body        ::= (expression ws)+

expression  ::= atom | let-expr | if-expr | func-call

let-expr    ::= "(" ws "let" ws symbol ws expression ws ")"
if-expr     ::= "(" ws "if" ws expression ws expression (ws expression)? ws ")"
func-call   ::= "(" ws symbol (ws expression)* ws ")"

atom        ::= number | string | symbol

type        ::= ":int" | ":float" | ":str" | ":bool" | ":void"

symbol      ::= [a-zA-Z_+*/-] [a-zA-Z0-9_+*/-]*
number      ::= [0-9]+
string      ::= "\"" [^"]* "\""